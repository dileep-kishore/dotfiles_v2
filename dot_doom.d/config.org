#+title: Doom Emacs literate config
#+author: Dileep Kishore
#+last_modified: [2021-12-18 Sat 14:02]
#+startup: overview

Making this file run a little faster
#+begin_src elisp
;;; config.el -*- lexical-binding: t; -*-
#+end_src

* Table of Contents :TOC:
- [[#configuring-private-settings][Configuring private settings]]
  - [[#enabling-ssh-agents][Enabling SSH agents]]
  - [[#org-mode-and-org-roam-basics][Org-mode and Org-roam basics]]
  - [[#setting-up-email-with-mu4e][Setting up email with =mu4e=]]
- [[#emacs-daemon][Emacs daemon]]
- [[#word-delimiters-and-text-objects][Word delimiters and text objects]]
  - [[#treat-_-as-a-word-character-in-various-programming-languages][Treat '_' as a word character in various programming languages]]
- [[#useful-functionsmacros-that-are-helpful-for-configuration][Useful functions/macros that are helpful for configuration]]
- [[#the-looks][The looks]]
  - [[#theme-configuration][Theme configuration]]
  - [[#modeline][Modeline]]
  - [[#font-configuration][Font configuration]]
  - [[#ligatures][Ligatures]]
  - [[#mixed-pitch-mode][Mixed pitch mode]]
  - [[#priorities][Priorities]]
  - [[#tags][Tags]]
  - [[#whitespace][Whitespace]]
  - [[#writeroom-configuration][Writeroom configuration]]
- [[#simple-settings][Simple settings]]
  - [[#battery-status-and-password-cache][Battery status and password-cache]]
  - [[#deleting-files-undo-limits-etc][Deleting files, undo limits, etc.]]
  - [[#rotate-layouts][Rotate layouts]]
  - [[#wakatime-mode][Wakatime-mode]]
  - [[#chezmoi-keybindings][Chezmoi keybindings]]
- [[#buffers-and-windows][Buffers and windows]]
  - [[#ivy-buffers][Ivy Buffers]]
  - [[#registers][Registers]]
- [[#dired][dired]]
  - [[#keybindings][Keybindings]]
- [[#auto-completions-and-snippets][Auto-completions and snippets]]
  - [[#auto-completions][Auto-completions]]
  - [[#snippets][Snippets]]
- [[#vim-and-evil-mode-configuration][Vim and Evil mode configuration]]
- [[#org-mode-and-org-roam-configuration][Org-mode and Org-roam configuration]]
  - [[#latex][Latex]]
  - [[#task-management][Task management]]
  - [[#org-agenda-and-org-super-agenda-configuration][Org-agenda and org-super-agenda configuration]]
  - [[#org-babel][Org-babel]]
  - [[#cancelled-support-lsp-in-src-blocks][CANCELLED Support =lsp= in =src= blocks]]
  - [[#org-roam][Org-roam]]
  - [[#org-transclusion][Org-transclusion]]
  - [[#org-mode-addons][Org-mode addons]]
  - [[#git-commit-keybinding][Git-commit keybinding]]
  - [[#keybinding-to-update-title-size][Keybinding to update title size]]
  - [[#org-books][Org-books]]
  - [[#org-gcal][Org-gcal]]
  - [[#org-toggl][Org-toggl]]
  - [[#org-krita][Org-Krita]]
- [[#language-specific-configuration][Language specific configuration]]
  - [[#plantuml][plantuml]]
  - [[#julia][Julia]]
  - [[#groovy-and-nextflow][Groovy and nextflow]]
- [[#credits][Credits]]

* Configuring private settings

Identify configuration for various functionality, e.g. *GPG* configuration, /email/, file templates and snippets
#+begin_src elisp
(setq user-full-name "Dileep Kishore"
      user-mail-address "k.dileep1994@gmail.com")
#+end_src

** Enabling SSH agents

By default doom blacklists =SSH_AUTH_SOCK= and =SSH_AUTH_PID= variables, which means ssh agents don't work.
#+begin_src elisp
(when noninteractive
  (add-to-list 'doom-env-whitelist "^SSH_"))
#+end_src

** Org-mode and Org-roam basics

#+begin_src elisp
(setq org-directory "~/Documents/Org"
      org-use-property-inheritance t              ; it's convenient to have properties inherited
      org-list-allow-alphabetical t               ; have a. A. a) A) list bullets
      org-export-in-background t                  ; run export processes in external emacs process
      org-catch-invisible-edits 'smart            ; try not to accidently do weird stuff in invisible regions
      org-export-with-sub-superscripts '{}        ; don't treat lone _ / ^ as sub/superscripts, require _{} / ^{}
      )
(setq org-roam-directory "~/Documents/Org/Roam")
(org-roam-db-autosync-mode)  ; needed to ensure org-roam is available on startup
#+end_src

Bullet sequences
#+begin_src elisp
(setq org-list-demote-modify-bullet '(("+" . "-") ("-" . "+") ("*" . "+") ("1." . "a.")))
#+end_src


** Setting up email with =mu4e=

I followed the instructions in the [[org:../../.emacs.d/modules/email/mu4e/README.org][mu4e documentation in Doom Emacs]].
The default mail directory is assumed to be =~/.mail=

This is the configuration for *Personal* email
#+begin_src elisp
(set-email-account! "Personal"
                    '((mu4e-sent-folder       . "/Personal/Sent Mail")
                      (mu4e-drafts-folder     . "/Personal/Drafts")
                      (mu4e-trash-folder      . "/Personal/Trash")
                      (mu4e-refile-folder     . "/Personal/All Mail")
                      (smtpmail-smtp-user     . "kdileep1994@gmail.com")
                      (user-mail-address      . "kdileep1994@gmail.com")    ;; only needed for mu < 1.4
                      (mu4e-compose-signature . "---\nBest,\nDileep Kishore"))
                    t)
#+end_src

This is the configuration for *Boston University* email
#+begin_src elisp
(set-email-account! "BU"
                    '((mu4e-sent-folder       . "/BU/Sent Mail")
                      (mu4e-drafts-folder     . "/BU/Drafts")
                      (mu4e-trash-folder      . "/BU/Trash")
                      (mu4e-refile-folder     . "/BU/All Mail")
                      (smtpmail-smtp-user     . "dkishore@bu.edu")
                      (user-mail-address      . "dkishore@bu.edu")    ;; only needed for mu < 1.4
                      (mu4e-compose-signature . "---\nDileep Kishore\nPh.D. Student | SegrÃ¨ Lab\nGraduate Program in Bioinformatics | Boston University"))
                    t)
#+end_src
* Emacs daemon

For running a =systemd= service for Emacs server
#+begin_src systemd :tangle no
[Unit]
Description=Emacs server daemon
Documentation=info:emacs man:emacs(1) https://gnu.org/software/emacs/

[Service]
Type=forking
ExecStart=sh -c 'emacs --daemon && emacsclient -c --eval "(delete-frame)"'
ExecStop=/usr/bin/emacsclient --no-wait --eval "(progn (setq kill-emacs-hook nil) (kill emacs))"
Restart=on-failure

[Install]
WantedBy=default.target
#+end_src

which is enabled by
#+begin_src shell :tangle no
systemctl --user enable emacs.service
#+end_src

Make =Emacs= the "default app" for opening files by adding a desktop entry
#+begin_src config :tangle no
[Desktop Entry]
Name=Emacs client
GenericName=Text Editor
Comment=A flexible platform for end-user applications
MimeType=text/english;text/plain;text/x-makefile;text/x-c++hdr;text/x-c++src;text/x-chdr;text/x-csrc;text/x-java;text/x-moc;text/x-pascal;text/x-tcl;text/x-tex;application/x-shellscript;text/x-c;text/x-c++;
Exec=emacsclient -create-frame --alternate-editor="" --no-wait %F
Icon=emacs
Type=Application
Terminal=false
Categories=TextEditor;Utility;
StartupWMClass=Emacs
Keywords=Text;Editor;
X-KDE-StartupNotify=false
#+end_src

Using =Emacs= from the =CLI=, saved to =~/.dotfiles/bin/emacs-cli.sh=
#+begin_src bash :tangle no
#!/usr/bin/env bash
force_tty=false
force_wait=false
stdin_mode=""

args=()

while :; do
case "$1" in
-t | -nw | --tty)
force_tty=true
shift ;;
-w | --wait)
force_wait=true
shift ;;
-m | --mode)
stdin_mode=" ($2-mode)"
shift 2 ;;
-h | --help)
echo -e "\033[1mUsage: e [-t] [-m MODE] [OPTIONS] FILE [-]\033[0m

Emacs client convenience wrapper.

\033[1mOptions:\033[0m
\033[0;34m-h, --help\033[0m            Show this message
\033[0;34m-t, -nw, --tty\033[0m        Force terminal mode
\033[0;34m-w, --wait\033[0m            Don't supply \033[0;34m--no-wait\033[0m to graphical emacsclient
\033[0;34m-\033[0m                     Take \033[0;33mstdin\033[0m (when last argument)
\033[0;34m-m MODE, --mode MODE\033[0m  Mode to open \033[0;33mstdin\033[0m with

Run \033[0;32memacsclient --help\033[0m to see help for the emacsclient."
exit 0 ;;
--*=*)
set -- "$@" "${1%%=*}" "${1#*=}"
shift ;;
*)
if [ "$#" = 0 ]; then
break; fi
args+=("$1")
shift ;;
esac
done

if [ ! "${#args[*]}" = 0 ] && [ "${args[-1]}" = "-" ]; then
unset 'args[-1]'
TMP="$(mktemp /tmp/emacsstdin-XXX)"
cat > "$TMP"
args+=(--eval "(let ((b (generate-new-buffer \"*stdin*\"))) (switch-to-buffer b) (insert-file-contents \"$TMP\") (delete-file \"$TMP\")${stdin_mode})")
fi

if [ -z "$DISPLAY" ] || $force_tty; then
# detect terminals with sneaky 24-bit support
if { [ "$COLORTERM" = truecolor ] || [ "$COLORTERM" = 24bit ]; } \
&& [ "$(tput colors 2>/dev/null)" -lt 257 ]; then
if echo "$TERM" | grep -q "^\w\+-[0-9]"; then
termstub="${TERM%%-*}"; else
termstub="${TERM#*-}"; fi
if infocmp "$termstub-direct" >/dev/null 2>&1; then
TERM="$termstub-direct"; else
TERM="xterm-direct"; fi # should be fairly safe
fi
emacsclient --tty -create-frame --alternate-editor="" "${args[@]}"
else
if ! $force_wait; then
args+=(--no-wait); fi
emacsclient -create-frame --alternate-editor="" "${args[@]}"
fi
#+end_src
* Word delimiters and text objects
** Treat '_' as a word character in various programming languages
#+begin_src elisp
;; For python
(add-hook! 'python-mode-hook (modify-syntax-entry ?_ "w"))
;; For ruby
(add-hook! 'ruby-mode-hook (modify-syntax-entry ?_ "w"))
;; For Javascript
(add-hook! 'js2-mode-hook (modify-syntax-entry ?_ "w"))
#+end_src

* Useful functions/macros that are helpful for configuration

- *load!* for loading external *.el files relative to this one
- *use-package!* for configuring packages
- *after!* for running code after a package has loaded
- *add-load-path!* for adding directories to the *load-path*, relative to
this file. Emacs searches the *load-path* when you load packages with
*require* or *use-package*.
- *map!* for binding new keys

To get information about any of these functions/macros, move the cursor over
the highlighted symbol at press =K= (non-evil users must press =C-c c k=).
This will open documentation for it, including demos of how they are used.

You can also try =gd= (or =C-c c d=) to jump to their definition and see how
they are implemented.

* The looks

** Theme configuration

There are two ways to load a /theme/ in *Doom Emacs*, assuming that it is installed and available
1. Set it using the =doom-theme= variable
2. Manually load it using the =load-theme= function

#+begin_src elisp
(setq doom-theme 'doom-palenight)
#+end_src

Nicer name for the default fallback buffer
#+begin_src elisp
(setq doom-fallback-buffer-name "â–º Doom"
      +doom-dashboard-name "â–º Doom")
#+end_src

Only show file-encoding when format is not =LF UTF-8= (source [[Credits][Tecosaur]])
#+begin_src elisp
(defun doom-modeline-conditional-buffer-encoding ()
  "We expect the encoding to be LF UTF-8, so only show the modeline when this is not the case"
  (setq-local doom-modeline-buffer-encoding
              (unless (and (memq (plist-get (coding-system-plist buffer-file-coding-system) :category)
                                 '(coding-category-undecided coding-category-utf-8))
                           (not (memq (coding-system-eol-type buffer-file-coding-system) '(1 2))))
                t)))

(add-hook 'after-change-major-mode-hook #'doom-modeline-conditional-buffer-encoding)
#+end_src

Simpler window title that shows if a file was modified:
#+begin_src elisp
(setq frame-title-format
      '(""
        (:eval
         (if (s-contains-p org-directory (or buffer-file-name ""))
             (replace-regexp-in-string
              ".*/[0-9]*-?" "â˜° "
              (subst-char-in-string ?_ ?  buffer-file-name))
           "%b"))
        (:eval
         (let ((project-name (projectile-project-name)))
           (unless (string= "-" project-name)
             (format (if (buffer-modified-p)  " â—‰ %s" " â— %s") project-name))))))
#+end_src
** Modeline

Remove Zettelkasten prefix from files while using =org-roam=
#+begin_src elisp
(defadvice! doom-modeline--buffer-file-name-roam-aware-a (orig-fun)
  :around #'doom-modeline-buffer-file-name ; takes no args
  (if (s-contains-p org-roam-directory (or buffer-file-name ""))
      (replace-regexp-in-string
       "\\(?:^\\|.*/\\)\\([0-9]\\{4\\}\\)\\([0-9]\\{2\\}\\)\\([0-9]\\{2\\}\\)[0-9]*-"
       "ðŸ¢”(\\1-\\2-\\3) "
       (subst-char-in-string ?_ ?  buffer-file-name))
    (funcall orig-fun)))
#+end_src

** Font configuration

*** Basic font configuration

Doom exposes five (optional) variables for controlling fonts:
- =doom-font=
- =doom-serif-font= (not super sure about this one)
- =doom-variable-pitch-font=
- =doom-unicode-font=
- =doom-big-font= - Used for =doom-big-font-mode= for presentations or streaming
#+begin_src elisp
(setq doom-font (font-spec :family "VictorMono Nerd Font" :size 16 :height 160)
      doom-variable-pitch-font (font-spec :family "Alegreya" :size 21 :weight 'extra-light)
      doom-unicode-font (font-spec :family "FiraCode Nerd Font")
      doom-serif-font (font-spec :family "BlexMono Nerd Font" :weight 'light)
      doom-big-font (font-spec :family "FiraCode Nerd Font" :size 25))
#+end_src

Code snippet to check if we are missing our required font (from [[Credits][Tecosaur]])
#+begin_src elisp
(defvar required-fonts '("VictorMono Nerd Font" "Overpass Nerd Font" "Alegreya" "FiraCode Nerd Font" "BlexMono Nerd Font"))

(defvar available-fonts
  (delete-dups (or (font-family-list)
                   (split-string (shell-command-to-string "fc-list : family")
                                 "[,\n]"))))

(defvar missing-fonts
  (delq nil (mapcar
             (lambda (font)
               (unless (delq nil (mapcar (lambda (f)
                                           (string-match-p (format "^%s$" font) f))
                                         available-fonts))
                 font))
             required-fonts)))

(if missing-fonts
    (pp-to-string
     `(unless noninteractive
        (add-hook! 'doom-init-ui-hook
          (run-at-time nil nil
                       (lambda ()
                         (message "%s missing the following fonts: %s"
                                  (propertize "Warning!" 'face '(bold warning))
                                  (mapconcat (lambda (font)
                                               (propertize font 'face 'font-lock-variable-name-face))
                                             ',missing-fonts
                                             ", "))
                         (sleep-for 0.5))))))
  ";; No missing fonts detected")
#+end_src

We can change the font used in the various faces across /Emacs/ using =custom-set-faces=.

There are several faces, some of them are:
- =font-lock-preprocessor-face=
- =font-lock-comment-face=
- =font-lock-keyword-face=
- =font-lock-comment-face=
- =font-lock-constant-face=
- =font-lock-function-name-face=
- =font-lock-keyword-face=
- =font-lock-preprocessor-face=
- =font-lock-string-face=
- =hl-todo=
- =info-colors-lisp-code-block=
- =markdown-code-face=

#+begin_src elisp
(custom-set-faces!
  '(font-lock-preprocessor-face :weight bold)
  '(font-lock-keyword-face :slant italic :weight bold))
#+end_src

*** Org-mode look customization

Hide emphasis markup, eg. *bold*, /italics/, +strikethrough+, ~highlight~
#+begin_src elisp
(setq org-startup-folded 'overview)
(setq org-hide-emphasis-markers t)
#+end_src

Change how the bullets look
#+begin_src elisp
(with-eval-after-load 'org-superstar
  (set-face-attribute 'org-superstar-item nil :height 1.15)
  (set-face-attribute 'org-superstar-header-bullet nil :height 1.15)
  (set-face-attribute 'org-superstar-leading nil :height 1.2))
(setq org-superstar-prettify-item-bullets t)
(setq org-superstar-headline-bullets-list '("âžŠ" "âž‹" "âžŒ" "âž" "âžŽ" "âž" "âž" "âž‘" "âž’"))
;; (setq org-superstar-headline-bullets-list '("âš" "â—‰" "âš" "â–" "â˜" "â™" "âœ¿" "âœ¸" "â—‹"))
;; (setq org-superstar-headline-bullets-list '("à¥§" "à¥¨" "à¥©" "à¥ª" "à¥«" "à¥¬" "à¥­" "à¥®" "à¥¯"))
;; (setq org-superstar-headline-bullets-list '("à³§" "à³¨" "à³©" "à³ª" "à³«" "à³¬" "à³­" "à³®" "à³¯"))
;; (setq org-superstar-headline-bullets-list '("ä¸€" "äºŒ" "ä¸‰" "å››" "äº”" "å…­" "ä¸ƒ" "å…«"))
(setq org-ellipsis "â€¦")
#+end_src

Make "quote" blocks /italic/
#+begin_src elisp
(setq org-fontify-quote-and-verse-blocks t)
#+end_src

*** Headings customization

Make headings slightly bigger
#+begin_src elisp
(custom-set-faces!
  '(outline-1 :weight extra-bold :height 1.25)
  '(outline-2 :weight bold :height 1.15)
  '(outline-3 :weight bold :height 1.12)
  '(outline-4 :weight semi-bold :height 1.09)
  '(outline-5 :weight semi-bold :height 1.06)
  '(outline-6 :weight semi-bold :height 1.03)
  '(outline-8 :weight semi-bold)
  '(outline-9 :weight semi-bold))
#+end_src

** Ligatures

#+begin_src elisp
(appendq! +ligatures-extra-symbols
          `(:src_block     "Â»"
            :src_block_end "Â«"
            :alias         "Ã…"
            :startup       "Â§"
            :archive       "ïš’ "
            :logbook       "âŸ³"
            :created:      "â˜¾"
            :last_modified:"âˆ‚"
            :export_file   "Ð„"
            :tags          "Ï„"
            :filetags      "Ï„"
            :checkbox      "â˜"
            :pending       "â– "
            :checkedbox    "â˜’"
            :list_property "âˆ·"
            :em_dash       "â€”"
            :ellipses      "â€¦"
            :arrow_right   "â†’"
            :arrow_left    "â†"
            :title         "å¹²"
            :subtitle      "t"
            :author        "â‚³"
            :date          "Ä"
            :property      "â›­"
            :options       "âŒ¥"
            :latex_class   "ðŸ„²"
            :latex_header  "â‡¥"
            :beamer_header "â† "
            :attr_latex    "ðŸ„›"
            :attr_html     "ðŸ„—"
            :begin_quote   "â"
            :end_quote     "âž"
            :caption       "â˜°"
            :header        "â€º"
            :results       "â„›"
            :begin_export  "â¤‡"
            :end_export    "â¤†"
            :properties    "â›­"
            :end           "âˆŽ"
            ))
(set-ligatures! 'org-mode
  :merge t
  :tags          "#+roam_tags"
  :filetags      "#+filetags"
  :alias         ":ROAM_ALIASES:"
  :created:      "#+created"
  :last_modified:"#+last_modified"
  :startup       "#+startup"
  :archive       "#+archive"
  :export_file   "#+export_file_name"
  :checkbox      "[ ]"
  :pending       "[-]"
  :checkedbox    "[X]"
  :list_property "::"
  :em_dash       "---"
  :ellipsis      "..."
  :arrow_right   "->"
  :arrow_left    "<-"
  :title         "#+title"
  :subtitle      "#+subtitle"
  :author        "#+author"
  :date          "#+date"
  :property      "#+property"
  :options       "#+options"
  :latex_class   "#+latex_class"
  :latex_header  "#+latex_header"
  :beamer_header "#+beamer_header"
  :attr_latex    "#+attr_latex"
  :attr_html     "#+attr_latex"
  :begin_quote   "#+begin_quote"
  :end_quote     "#+end_quote"
  :caption       "#+caption"
  :header        "#+header"
  :begin_export  "#+begin_export"
  :end_export    "#+end_export"
  :results       "#+RESULTS"
  :property      ":PROPERTIES:"
  :logbook       ":LOGBOOK:"
  :end           ":END:"
  )
(plist-put! +ligatures-extra-symbols
            :name          "â"
            :true          "Å¦"
            :false         "â‚£"
            :pipe          "â¯ˆ")
#+end_src

** Mixed pitch mode

Org-mode hook for pretty =unicode= header symbols and =mixed pitch font=
- I think the =unicode= header symbols are automatically included with the =org +pretty= flag in ~init.el~, hence adding another hook for that is redundant.
- Finally, we enable =visual-line-mode= and =org-appear-mode=
#+begin_src elisp
(add-hook! 'org-mode-hook #'+org-pretty-mode #'mixed-pitch-mode #'visual-line-mode)
(add-hook! (gfm-mode markdown-mode) #'mixed-pitch-mode #'visual-line-mode)
#+end_src

Disable =org-appear-autolinks=
#+begin_src elisp
(use-package! org-appear
  :hook (org-mode . org-appear-mode)
  :config
  (setq org-appear-autoemphasis t
        org-appear-autosubmarkers nil
        org-appear-autolinks nil)
  ;; for proper first-time setup, `org-appear--set-elements'
  ;; needs to be run after other hooks have acted.
  (run-at-time nil nil #'org-appear--set-elements))
#+end_src

Use ~Alegreya~ font as =variable-pitch-serif-font=
#+begin_src elisp
(autoload #'mixed-pitch-serif-mode "mixed-pitch"
  "Change the default face of the current buffer to a serifed variable pitch, while keeping some faces fixed pitch." t)

(after! mixed-pitch
  (defface variable-pitch-serif
    '((t (:family "serif")))
    "A variable-pitch face with serifs."
    :group 'basic-faces)
  (setq mixed-pitch-set-height t)
  (set-face-attribute 'org-hide nil :inherit 'fixed-pitch)
  (set-face-attribute 'org-quote nil :background "#1e2030" :extend t :inherit 'variable-pitch-serif)
  (set-face-attribute 'org-document-title nil
                      :height 1.9
                      :weight 'bold)
  (setq variable-pitch-serif-font (font-spec :family "Alegreya" :size 21 :weight 'extra-light))
  (set-face-attribute 'variable-pitch-serif nil :font variable-pitch-serif-font)
  (defun mixed-pitch-serif-mode (&optional arg)
    "Change the default face of the current buffer to a serifed variable pitch, while keeping some faces fixed pitch."
    (interactive)
    (let ((mixed-pitch-face 'variable-pitch-serif))
      (mixed-pitch-mode (or arg 'toggle)))))
#+end_src

Change header sizes in =org-mode=.
#+begin_src elisp
(after! org
  (set-face-attribute 'org-link nil :slant 'italic)
  (set-face-attribute 'org-block nil :inherit 'fixed-pitch)
  (set-face-attribute 'org-hide nil :inherit 'fixed-pitch)
  (set-face-attribute 'org-code nil :inherit '(shadow fixed-pitch))
  ;; (set-face-attribute 'org-quote nil :inherit 'variable-pitch-serif)
  (set-face-attribute 'org-quote nil :background "#1e2030" :extend t :inherit 'variable-pitch-serif)
  (set-face-attribute 'org-document-title nil
                      :height 1.9
                      :weight 'bold))
#+end_src

** Priorities
#+begin_src elisp
(after! org
  (setq
   org-highest-priority ?A
   org-default-priority ?B
   org-lowest-priority ?E)
  (setq org-priority-faces '((?A :foreground "red")
                             (?B :foreground "orange")
                             (?C :foreground "yellow")
                             (?D :foreground "green")
                             (?E :foreground "gray"))))
(use-package! org-fancy-priorities
  :hook
  (org-mode . org-fancy-priorities-mode)
  :config
  (setq org-fancy-priorities-list '("ðŸ„°" "ðŸ„±" "ðŸ„²" "ðŸ„³" "ðŸ„´")))
(add-hook 'org-agenda-mode-hook 'org-fancy-priorities-mode)
#+end_src

=org-pretty-table= configuration
#+begin_src elisp
(use-package! org-pretty-table
  :commands (org-pretty-table-mode global-org-pretty-table-mode))
#+end_src
** Tags

Default global tags list, using PARA method keywords
#+begin_src elisp
(setq org-tag-alist
      '(
        ("must_do")
        ("should_do")
        ("could_do")
        ("impact_high")
        ("impact_med")
        ("impact_low")
        ("effort_high")
        ("effort_med")
        ("effort_low")
        ("work" . ?w)
        ("home" . ?h)
        ("university" . ?u)
        ("writing")
        ("coding" . ?c)
        ("science" . ?s)
        ("video")
        ("mail" . ?m)
        ("social")
        ("read" . ?r)
        ("article" . ?a)
        ("web")
        ("shop")
        ("info")
        ("issue")
        ("someday")
        ("idea" . ?i)
        ("fitness")
        ("health")
        ("dotfiles")
        ("emacs")))
#+end_src

Pretty tags using =org-pretty-tags=
#+begin_src elisp
(use-package org-pretty-tags
  :config
  (setq org-pretty-tags-surrogate-strings
        `(
          ("must_do" . ,(concat
                         (all-the-icons-octicon   "alert" :face 'all-the-icons-blue  :v-adjust 0.01)
                         (all-the-icons-octicon   "alert" :face 'all-the-icons-blue  :v-adjust 0.01)
                         (all-the-icons-octicon   "alert" :face 'all-the-icons-blue  :v-adjust 0.01)
                         ))
          ("should_do" . ,(concat
                           (all-the-icons-octicon   "alert" :face 'all-the-icons-blue  :v-adjust 0.01)
                           (all-the-icons-octicon   "alert" :face 'all-the-icons-blue  :v-adjust 0.01)
                           ))
          ("could_do" . ,(concat
                          (all-the-icons-octicon   "alert" :face 'all-the-icons-blue  :v-adjust 0.01)
                          ))
          ("impact_high" . ,(concat
                             (all-the-icons-faicon   "fire" :face 'all-the-icons-red  :v-adjust 0.01)
                             (all-the-icons-faicon   "fire" :face 'all-the-icons-red  :v-adjust 0.01)
                             (all-the-icons-faicon   "fire" :face 'all-the-icons-red  :v-adjust 0.01)
                             ))
          ("impact_med" . ,(concat
                            (all-the-icons-faicon   "fire" :face 'all-the-icons-red  :v-adjust 0.01)
                            (all-the-icons-faicon   "fire" :face 'all-the-icons-red  :v-adjust 0.01)
                            ))
          ("impact_low" . ,(concat
                            (all-the-icons-faicon   "fire" :face 'all-the-icons-red  :v-adjust 0.01)
                            ))
          ("effort_high" . ,(concat
                             (all-the-icons-faicon   "coffee" :face 'all-the-icons-yellow  :v-adjust 0.01)
                             (all-the-icons-faicon   "coffee" :face 'all-the-icons-yellow  :v-adjust 0.01)
                             (all-the-icons-faicon   "coffee" :face 'all-the-icons-yellow  :v-adjust 0.01)
                             ))
          ("effort_med" . ,(concat
                            (all-the-icons-faicon   "coffee" :face 'all-the-icons-yellow  :v-adjust 0.01)
                            (all-the-icons-faicon   "coffee" :face 'all-the-icons-yellow  :v-adjust 0.01)
                            ))
          ("effort_low" . ,(concat
                            (all-the-icons-faicon   "coffee" :face 'all-the-icons-yellow  :v-adjust 0.01)
                            ))
          ("TOC"      . ,(all-the-icons-faicon   "table"           :face 'all-the-icons-purple  :v-adjust 0.01))
          ("home"      . ,(all-the-icons-faicon   "home"           :face 'all-the-icons-purple  :v-adjust 0.01))
          ("work"      . ,(all-the-icons-material   "work"           :face 'all-the-icons-purple  :v-adjust 0.01))
          ("university" . ,(all-the-icons-faicon   "graduation-cap" :face 'all-the-icons-purple  :v-adjust 0.01))
          ("writing"   . ,(all-the-icons-faicon   "pencil"          :face 'all-the-icons-silver  :v-adjust 0.01))
          ("coding"    . ,(all-the-icons-faicon   "code"           :face 'all-the-icons-silver  :v-adjust 0.01))
          ("science"    . ,(all-the-icons-faicon   "flask"         :face 'all-the-icons-silver  :v-adjust 0.01))
          ("video"      . ,(all-the-icons-material "movie"         :face 'all-the-icons-orange  :v-adjust 0.01))
          ("mail"      . ,(all-the-icons-faicon   "envelope"       :face 'all-the-icons-blue    :v-adjust 0.01))
          ("social"      . ,(all-the-icons-faicon   "facebook"       :face 'all-the-icons-blue    :v-adjust 0.01))
          ("read"       . ,(all-the-icons-octicon  "book"           :face 'all-the-icons-lblue   :v-adjust 0.01))
          ("article"    . ,(all-the-icons-octicon  "file-text"      :face 'all-the-icons-yellow  :v-adjust 0.01))
          ("web"        . ,(all-the-icons-faicon   "globe"          :face 'all-the-icons-green   :v-adjust 0.01))
          ("shop"        . ,(all-the-icons-faicon   "shopping-cart" :face 'all-the-icons-green   :v-adjust 0.01))
          ("info"       . ,(all-the-icons-faicon   "info-circle"    :face 'all-the-icons-blue    :v-adjust 0.01))
          ("issue"      . ,(all-the-icons-faicon   "bug"            :face 'all-the-icons-red     :v-adjust 0.01))
          ("someday"    . ,(all-the-icons-faicon   "calendar-o"     :face 'all-the-icons-cyan    :v-adjust 0.01))
          ("idea"       . ,(all-the-icons-faicon  "lightbulb-o"     :face 'all-the-icons-yellow  :v-adjust 0.01))
          ("fitness"   . ,(all-the-icons-faicon "bolt"      :face 'all-the-icons-yellow :v-adjust 0.01))
          ("health"   . ,(all-the-icons-material "local_hospital"      :face 'all-the-icons-yellow :v-adjust 0.01))
          ("dotfiles"   . ,(all-the-icons-fileicon "arch-linux"      :face 'all-the-icons-lpurple :v-adjust 0.01))
          ("emacs"      . ,(all-the-icons-fileicon "emacs"          :face 'all-the-icons-lpurple :v-adjust 0.01))))
  (org-pretty-tags-global-mode))
#+end_src

** Whitespace

Set =global whitespace mode=
#+begin_src elisp
(setq
 global-whitespace-mode t
 whitespace-style '(face tabs tab-mark trailing newline newline-mark indentation))
(global-whitespace-mode +1)
(whitespace-mode +1)
#+end_src

** Writeroom configuration
- source :: https://tecosaur.github.io/emacs-config/config.html#writeroom

#+begin_src elisp
(setq +zen-text-scale 0.8)
(defvar +zen-serif-p t
  "Whether to use a serifed font with `mixed-pitch-mode'.")
(after! writeroom-mode
  (defvar-local +zen--original-org-indent-mode-p nil)
  (defvar-local +zen--original-mixed-pitch-mode-p nil)
  (defvar-local +zen--original-solaire-mode-p nil)
  (defvar-local +zen--original-org-pretty-table-mode-p nil)
  (defun +zen-enable-mixed-pitch-mode-h ()
    "Enable `mixed-pitch-mode' when in `+zen-mixed-pitch-modes'."
    (when (apply #'derived-mode-p +zen-mixed-pitch-modes)
      (if writeroom-mode
          (progn
            (setq +zen--original-solaire-mode-p solaire-mode)
            (solaire-mode -1)
            (setq +zen--original-mixed-pitch-mode-p mixed-pitch-mode)
            (funcall (if +zen-serif-p #'mixed-pitch-serif-mode #'mixed-pitch-mode) 1))
        (funcall #'mixed-pitch-mode (if +zen--original-mixed-pitch-mode-p 1 -1))
        (when +zen--original-solaire-mode-p (solaire-mode 1)))))
  (pushnew! writeroom--local-variables
            'visual-fill-column-width)
  (add-hook 'writeroom-mode-enable-hook
            (defun +zen-prose-org-h ()
              "Reformat the current Org buffer appearance for prose."
              (whitespace-mode -1)
              (when (eq major-mode 'org-mode)
                visual-fill-column-width 100
                (setq
                 +zen--original-org-pretty-table-mode-p (bound-and-true-p org-pretty-table-mode))
                (org-pretty-table-mode 1))))
  (add-hook 'writeroom-mode-disable-hook
            (defun +zen-nonprose-org-h ()
              "Reverse the effect of `+zen-prose-org'."
              (whitespace-mode +1)
              (when (eq major-mode 'org-mode)
                ;; (unless +zen--original-org-pretty-table-mode-p (org-pretty-table-mode -1))
                ))))
#+end_src

#+RESULTS:

* Simple settings

** Battery status and password-cache

#+begin_src elisp
(if (equal "Battery status not available"
           (battery))
    (display-battery-mode 1)  ; On laptops it's nice to know how much power you have
  (setq password-cache-expiry nil)) ; no battery = desktop
#+end_src

** Deleting files, undo limits, etc.

- Delete files to trash
- Take new window space from all other windows (not just current)
- Stretch cursor to glyph width (for variable-pitch-font)
#+begin_src elisp
(setq-default
 delete-by-moving-to-trash t
 window-combination-resize t
 x-stretch-cursor t)
#+end_src

- Raise the undow limit to =80Mb=
- Granular undos in insert mode!
- Unicde ellipsis (â€¦ )
#+begin_src elisp
(setq undo-limit 80000000
      evil-want-fine-undo t
      truncate-string-ellipsis "â€¦")
#+end_src

** Rotate layouts

Add keybinding to rotate the layout of all windows
#+begin_src elisp
(map! :map evil-window-map
      "SPC" #'rotate-layout)
#+end_src

** Wakatime-mode

Enable =wakatime-mode= by default
#+begin_src elisp
(global-wakatime-mode)
#+end_src

** Chezmoi keybindings

Keybinding to sync dotfiles using chezmoi
#+begin_src elisp
(defun czy ()
  " Apply changes to files using chezmoi "
  (interactive)
  (shell-command "chezmoi apply"))

(map! :leader
      :prefix "z"
      :desc "chezmoi apply" "y" #'czy)
#+end_src

* Buffers and windows

** Ivy Buffers

Make =ivy= open buffers in /horizontal/ and /vertical/ splits using ~M-o |~ and ~M-o %~
TODO These don't work yet. The problem is going from filename to file location and jumping to it

#+begin_src elisp :tangle no
(defun find-file-right (filename)
  (interactive)
  (split-window-right)
  (other-window 1)
  (find-file filename))

(defun find-file-below (filename)
  (interactive)
  (split-window-below)
  (other-window 1)
  (find-file filename))

(ivy-set-actions
 'counsel-find-file
 '(("|" find-file-right "open right")
   ("%" find-file-below "open below")))

(ivy-set-actions
 'counsel-recentf
 '(("|" find-file-right "open right")
   ("%" find-file-below "open below")))

(ivy-set-actions
 'counsel-buffer-or-recentf
 '(("|" find-file-right "open right")
   ("%" find-file-below "open below")))

(ivy-set-actions
 'ivy-switch-buffer
 '(("|" find-file-right "open right")
   ("%" find-file-below "open below")))
#+end_src
** Registers

By default ~Doom Emacs~ uses =C-x r= for register, we want to use =SPC r=
#+begin_src elisp
(map! :leader
      (:prefix ("r" . "registers")
       :desc "Copy to register" "c" #'copy-to-register
       :desc "Frameset to register" "f" #'frameset-to-register
       :desc "Insert contents of register" "i" #'insert-register
       :desc "Jump to register" "j" #'jump-to-register
       :desc "List registers" "l" #'list-registers
       :desc "Number to register" "n" #'number-to-register
       :desc "Interactively choose a register" "r" #'counsel-register
       :desc "View a register" "v" #'view-register
       :desc "Window configuration to register" "w" #'window-configuration-to-register
       :desc "Increment register" "+" #'increment-register
       :desc "Point to register" "SPC" #'point-to-register))
#+end_src

* dired

** Keybindings

We will be using =SPC d= for all =dired= keybindings (since ~Doom Emacs~) doesn't use that for anything else
#+begin_src elisp
(map! :leader
      (:prefix ("d" . "dired")
       :desc "Open dired" "d" #'dired
       :desc "Dired jump to current" "j" #'dired-jump))
;; Make 'h' and 'l' go back and forward in dired. Much faster to navigate the directory structure!
(evil-define-key 'normal dired-mode-map
  (kbd "h") 'dired-up-directory
  (kbd "l") 'dired-find-file) ; use dired-find-file instead if not using dired-open package
;; If peep-dired is enabled, you will get image previews as you go up/down with 'j' and 'k'
;; Get file icons in dired
(add-hook 'dired-mode-hook 'all-the-icons-dired-mode)
#+end_src

* Auto-completions and snippets

** Auto-completions

Decrease =company-mode= completion delay and other nice things ðŸ¤£ (also I just figured out how to insert emoji).
#+begin_src elisp
(after! company
  (setq company-idle-delay 0
        company-minimum-prefix-length 2)
  (setq company-show-numbers t))
#+end_src

Improve =prescient= usage by increasing history
#+begin_src elisp
(setq-default history-length 1000)
(setq-default prescient-history-length 1000)
#+end_src

Having =ispell= enabled in =text=, =markdown= and =GFM=
#+begin_src elisp
(setq ispell-dictionary "en")
(set-company-backend!
  '(text-mode
    markdown-mode
    gfm-mode)
  '(:seperate
    company-ispell
    company-files
    company-yasnippet))
#+end_src

*** Tabnine integration
#+begin_src elisp
(use-package! company-tabnine
  :ensure t
  :config
  (add-to-list 'company-backends 'company-tabnine))
(setq +lsp-company-backends '(company-lsp company-tabnine :separate company-capf company-yasnippet company-dabbrev company-dabbrev-code))
#+end_src

** Snippets

*** Basic configuration

Change the default snippets directory
#+begin_src elisp
(setq +snippets-dir "/home/dileep/.dotfiles/dot_doom.d/snippets")
#+end_src

*** Documentation snippets

=Numpydoc= documentation snippet plugin
#+begin_src elisp
(use-package numpydoc
  :bind (:map python-mode-map
         ("C-c C-n" . numpydoc-generate)))
(setq numpydoc-insertion-style 'yas)
(setq numpydoc-insert-examples-block nil)
#+end_src

* Vim and Evil mode configuration

This determines the style of the /line numbers/
#+begin_src elisp
(setq display-line-numbers-type 'visual)
#+end_src

* Org-mode and Org-roam configuration

** Latex

Enable \LaTeX preview on startup (disabled because it's slow)
#+begin_src elisp :tangle no
(setq org-startup-with-latex-preview t)
#+end_src

1. Turn on =cdlatex= minor mode by default
2. Edit \LaTeX environment after inserting using =C-c }=
#+begin_src elisp
(after! org
  (add-hook 'org-mode-hook 'turn-on-org-cdlatex)
  (defadvice! org-edit-latex-emv-after-insert ()
    :after #'org-cdlatex-environment-indent
    (org-edit-latex-environment)))
#+end_src

Additional LaTeX packages to use:
#+begin_src elisp
;; (add-to-list 'org-latex-packages-alist '("" "minted"))
(after! org
  (setq org-latex-listings 'minted)
  (setq org-latex-minted-options
        '(("linenos" "true")
          ("breaklines" "true")
          ("breakanywhere" "true")))
  )
#+end_src

Prettier LaTeX in =org-mode= and removing the =org-block= face
#+begin_src elisp
(after! org
  (setq org-highlight-latex-and-related '(native script entities))
  (add-to-list 'org-src-block-faces '("latex" (:inherit default :extend t)))
  )
#+end_src

*** Better math rendering

Enable automatic LaTeX fragment previews
#+begin_src elisp
(use-package! org-fragtog
  :hook (org-mode . org-fragtog-mode))
#+end_src

#+begin_src elisp
(after! org
  (setq org-format-latex-header "\\documentclass{article}
\\usepackage[usenames]{xcolor}

\\usepackage[T1]{fontenc}

\\usepackage{booktabs}

\\pagestyle{empty}             % do not remove
% The settings below are copied from fullpage.sty
\\setlength{\\textwidth}{\\paperwidth}
\\addtolength{\\textwidth}{-3cm}
\\setlength{\\oddsidemargin}{1.5cm}
\\addtolength{\\oddsidemargin}{-2.54cm}
\\setlength{\\evensidemargin}{\\oddsidemargin}
\\setlength{\\textheight}{\\paperheight}
\\addtolength{\\textheight}{-\\headheight}
\\addtolength{\\textheight}{-\\headsep}
\\addtolength{\\textheight}{-\\footskip}
\\addtolength{\\textheight}{-3cm}
\\setlength{\\topmargin}{1.5cm}
\\addtolength{\\topmargin}{-2.54cm}
% my custom stuff
% \\usepackage{arev}
")
  (setq org-format-latex-options (plist-put org-format-latex-options :background "Transparent"))
  (setq org-format-latex-options (plist-put org-format-latex-options :scale 1.8))
  )
#+end_src

*** Numbering equations

#+begin_src elisp
;; Numbered equations all have (1) as the number for fragments with vanilla
;; org-mode. This code injects the correct numbers into the previews so they
;; look good.
(defun scimax-org-renumber-environment (orig-func &rest args)
  "A function to inject numbers in LaTeX fragment previews."
  (let ((results '())
        (counter -1)
        (numberp))
    (setq results (cl-loop for (begin . env) in
                           (org-element-map (org-element-parse-buffer) 'latex-environment
                             (lambda (env)
                               (cons
                                (org-element-property :begin env)
                                (org-element-property :value env))))
                           collect
                           (cond
                            ((and (string-match "\\\\begin{equation}" env)
                                  (not (string-match "\\\\tag{" env)))
                             (cl-incf counter)
                             (cons begin counter))
                            ((string-match "\\\\begin{align}" env)
                             (prog2
                                 (cl-incf counter)
                                 (cons begin counter)
                               (with-temp-buffer
                                 (insert env)
                                 (goto-char (point-min))
                                 ;; \\ is used for a new line. Each one leads to a number
                                 (cl-incf counter (count-matches "\\\\$"))
                                 ;; unless there are nonumbers.
                                 (goto-char (point-min))
                                 (cl-decf counter (count-matches "\\nonumber")))))
                            (t
                             (cons begin nil)))))

    (when (setq numberp (cdr (assoc (point) results)))
      (setf (car args)
            (concat
             (format "\\setcounter{equation}{%s}\n" numberp)
             (car args)))))

  (apply orig-func args))


(defun scimax-toggle-latex-equation-numbering ()
  "Toggle whether LaTeX fragments are numbered."
  (interactive)
  (if (not (get 'scimax-org-renumber-environment 'enabled))
      (progn
        (advice-add 'org-create-formula-image :around #'scimax-org-renumber-environment)
        (put 'scimax-org-renumber-environment 'enabled t)
        (message "Latex numbering enabled"))
    (advice-remove 'org-create-formula-image #'scimax-org-renumber-environment)
    (put 'scimax-org-renumber-environment 'enabled nil)
    (message "Latex numbering disabled.")))

(after! org
  (advice-add 'org-create-formula-image :around #'scimax-org-renumber-environment)
  (put 'scimax-org-renumber-environment 'enabled t)
  )
#+end_src
*** Compiling

#+begin_src elisp
;; org-latex-compilers = ("pdflatex" "xelatex" "lualatex"), which are the possible values for %latex
(after! org
  (setq org-latex-pdf-process '("latexmk -f -pdf -%latex -shell-escape -interaction=nonstopmode -output-directory=%o %f"))
  )
#+end_src

** Task management

Enable logging of done tasks, and log stuff into LOGBOOK drawer by default
#+begin_src elisp
(after! org
  (setq org-log-done 'time)
  (setq org-log-into-drawer t)
  ;; Don't log when changing state with shift-arrows
  ;; (setq org-treat-S-cursor-todo-selection-as-state-change nil)
  ;; Update org-todo keywords
  (setq org-todo-keywords '((sequence "TODO(t)" "PROG(p)"  "NEXT(n)" "WAIT(w@/!)" "REVIEW(r@/!)" "MISSION(m)" "OBJ(o)" "KR(k)" "|" "DONE(d)" "CANCELLED(c)" "DONE:MISSION(M)" "DONE:OBJ(O)" "DONE:KR(K)")))
  (setq
   org-todo-keyword-faces
   '(("TODO" :foreground "#7c7c75" :weight normal :underline t)
     ("PROG" :foreground "#0098dd" :weight normal :underline t)
     ("NEXT" :foreground "#9f7efe" :weight normal :underline t)
     ("WAIT" :foreground "#c5a89d" :weight normal :underline t)
     ("REVIEW" :foreground "#5ac18e" :weight normal :underline t)
     ("MISSION" :foreground "#ff7373" :weight bold :underline t)
     ("OBJ" :foreground "#ff8000" :weight bold :underline t)
     ("KR" :foreground "#569099" :weight bold :underline t)
     ("DONE" :foreground "#50a14f" :weight normal :underline t)
     ("DONE:MISSION" :foreground "#50a14f" :weight normal :underline t)
     ("DONE:OBJ" :foreground "#50a14f" :weight normal :underline t)
     ("DONE:KR" :foreground "#50a14f" :weight normal :underline t)
     ("CANCELLED" :foreground "#ff6480" :weight normal :underline t)))
  ;; Ensure that sub-tasks must be completed before parent can be marked done
  (setq org-enforce-todo-dependencies t))
#+end_src

Turn on auto-revert mode in org mode files so that they automatically update when changed (e.g. by =syncthing=)
#+begin_src elisp
(add-hook 'org-mode-hook 'auto-revert-mode)
#+end_src

** Org-agenda and org-super-agenda configuration
- source :: https://www.tquelch.com/posts/emacs-config/#agenda

Set up the hook for =org-super-agenda=
#+begin_src elisp
(setq org-agenda-start-day nil)
(setq org-agenda-start-on-weekday 1)
(setq org-agenda-span 'week)
(org-edna-mode)
(use-package! org-super-agenda
  :hook (org-agenda-mode . org-super-agenda-mode)
  :custom-face (org-super-agenda-header ((default (:inherit propositum-agenda-heading))))
  :config (setq org-agenda-start-day nil))

(after! (org-agenda org-super-agenda)
  (setq org-agenda-files (directory-files-recursively "~/Documents/Org/Roam/tasks/" "\\.org$"))
  (add-to-list 'org-agenda-files "~/Documents/Org/Roam/calendars")
  (setq! org-super-agenda-header-map (make-sparse-keymap)))
#+end_src

Org agenda keybinding
#+begin_src elisp
(map! :leader "a" #'org-agenda)
#+end_src

Org agenda defaults
#+begin_src elisp
(setq org-agenda-skip-scheduled-if-done t
      org-agenda-skip-deadline-if-done t
      org-agenda-include-deadlines t
      org-agenda-log-mode-items '(closed clock state)
      ;; org-agenda-block-separator nil
      org-agenda-tags-column 100 ;; from testing this seems to be a good value
      org-agenda-compact-blocks t
      org-agenda-dim-blocked-tasks nil
      org-agenda-start-with-log-mode t)
#+end_src

When opening an item from the agenda, ensure the whole tree (parents and siblings) is visible
#+begin_src elisp
(after! org-agenda
  (add-hook 'org-agenda-after-show-hook 'org-reveal))
#+end_src

Remap movement keys to move between agenda items rather than between lines
#+begin_src elisp
(map! :map org-agenda-mode-map
      [remap org-agenda-next-line] #'org-agenda-next-item
      [remap org-agenda-previous-line] #'org-agenda-previous-item)
#+end_src

*** Org-habit

#+begin_src elisp
(after! org-habit
  (setq org-habit-preceding-days 7)
  (setq org-habit-following-days 7)
  (setq +org-habit-graph-window-ratio 0.2)
  )
#+end_src

*** Org-agenda custom commands

#+begin_src elisp
(setq org-agenda-custom-commands
      '(("d" "Agenda"
         ((agenda "" ((org-agenda-span 'day)
                      (org-super-agenda-groups
                       '((:name "Today"
                          :time-grid t
                          :date today
                          :scheduled today
                          :order 1)))))
          (alltodo "" ((org-agenda-overriding-header "All scheduled tasks")
                       (org-super-agenda-groups
                        '((:auto-planning t)
                          (:discard (:anything t))))))))
        ("p" "Projects"
         ((agenda "" ((org-agenda-span 'day)
                      (org-super-agenda-groups
                       '((:name "Today"
                          :time-grid t
                          :date today
                          :scheduled today
                          :order 1)))))
          (alltodo "" ((org-agenda-overriding-header "")
                       (org-super-agenda-groups
                        '((:auto-outline-path t)
                          (:discard (:anything t))))))))
        ("o" "Overview"
         ((agenda "" ((org-agenda-span 'day)
                      (org-super-agenda-groups
                       '((:name "Today"
                          :time-grid t
                          :date today
                          :scheduled today
                          :order 1)))))
          (alltodo "" ((org-agenda-overriding-header "")
                       (org-super-agenda-groups
                        '((:name "Clocked today"
                           :log t)
                          (:name "Missions"
                           :and (:todo "MISSION" :deadline t)
                           :order 1)
                          (:name "Objectives"
                           :and (:todo "OBJ" :deadline t)
                           :order 1)
                          (:name "Key Results"
                           :and (:todo "KR" :deadline t)
                           :order 2)
                          (:name "Overdue"
                           :deadline past
                           :face error
                           :order 3)
                          (:name "Incomplete"
                           :scheduled past
                           :face error
                           :order 4)
                          (:name "Next to do"
                           :todo "NEXT"
                           :order 5)
                          (:name "Due Today"
                           :deadline today
                           :order 6)
                          (:name "Urgent"
                           :tag "Urgent"
                           :order 7)
                          (:name "Important"
                           :tag "Important"
                           :priority "A"
                           :order 8)
                          (:name "Due Soon"
                           :deadline future
                           :order 9)
                          (:name "To read"
                           :tag "Read"
                           :order 10)
                          (:name "Quick Picks"
                           :effort< "0:30"
                           :order 11)
                          (:name "Issues"
                           :tag "Issue"
                           :order 12)
                          (:name "Waiting"
                           :todo "WAITING"
                           :order 20)
                          (:name "University"
                           :tag "uni"
                           :order 32)
                          (:name "Trivial"
                           :priority<= "E"
                           :tag ("Trivial" "Unimportant")
                           :todo ("SOMEDAY" )
                           :order 90)
                          (:discard (:tag ("Chore" "Routine" "Daily")))
                          (:discard (:anything t))))))))))
#+end_src

*** Org-agenda looks

Make deadlines have the error face when they've passed
#+begin_src elisp
(setq org-agenda-deadline-faces
      '((1.001 . error)
        (1.0 . org-warning)
        (0.5 . org-upcoming-deadline)
        (0.0 . org-upcoming-distant-deadline)))
#+end_src
*** Org-clock commands

Persist clocks on Emacs restarts
#+begin_src elisp
(after! org-clock
  (setq org-clock-persist t)
  (org-clock-persistence-insinuate))
#+end_src
*** elegant-agenda-mode

#+begin_src elisp
(use-package elegant-agenda-mode
  :hook org-agenda-mode-hook)
#+end_src
** Org-babel

#+begin_src elisp
(after! org-babel
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((emacs-lisp . t)
     (julia . t)
     (python . t)
     (bash . t)
     (shell . t)
     (latex . t)
     (C . t)
     (dot . t)
     (plantuml . t)
     (makefile . t)
     (ein . t)
     (jupyter . t)))
  )
#+end_src

** CANCELLED Support =lsp= in =src= blocks

=lsp-mode= already provides experimental support for this. Just use =M-x lsp-org=

** Org-roam

You might want to refer to the [[https://www.orgroam.com/manual.html#The-Templating-System][templating system documentation]] for details. This has been updated for v2

Set up the default directories
#+begin_src elisp
(after! org-roam
  (setq org-roam-directory "~/Documents/Org/Roam")
  (setq org-roam-file-exclude-regexp "~/Documents/Org/Roam/tasks/archives/*")
  (setq +org-roam-open-buffer-on-find-file nil)
  )
#+end_src

Keybindings
#+begin_src elisp
(map! :leader
      (:prefix-map ("n" . "notes")
       (:when (featurep! :lang org +roam)
        (:prefix ("r" . "roam")
         :desc "toggle buffer" "t" #'org-roam-buffer-toggle-display))))
#+end_src

*** Org-roam dailies

#+begin_src elisp
(after! org-roam
  (setq org-roam-dailies-directory "dailies/")

  (defun get-string-from-file (filePath)
    "Return filePath's file content."
    (with-temp-buffer
      (insert-file-contents filePath)
      (buffer-string)))

  (setq org-roam-dailies-capture-templates
        (let
            ((day_template (get-string-from-file "~/Documents/Org/Roam/_templates/dailies-template.org"))
             (week_template (get-string-from-file "~/Documents/Org/Roam/_templates/weekly-template.org")))
          `(("j" "journaling" entry "- %?" :if-new
             (file+head "%<%Y-%m-%d>.org" ,day_template)
             :empty-lines-before 1
             :olp ("Journaling"))
            ("p" "plan" entry "** TODO %<%H:%M> %?" :if-new
             (file+head "%<%Y-%m-%d>.org" ,day_template)
             :empty-lines-before 1
             :olp ("Day Planner"))
            ("t" "thoughts" entry "- %<%H:%M> %?" :if-new
             (file+head "%<%Y-%m-%d>.org" ,day_template)
             :empty-lines-before 1
             :olp ("Thoughts"))
            ("w" "weekly" entry "%?" :if-new
             (file+head "%<%Y-w%U>.org" ,week_template)
             :empty-lines-before 1
             :olp ("How was your week? General thoughts")))))
  )
#+end_src

*** Org-roam capture templates

1. Templates for general notes
2. Templates for capturing references from the web using =org-protocol=
#+begin_src elisp
(after! org-roam

  (setq org-roam-capture-templates
        '(("d" "default" plain "%?" :if-new
           (file+head "%<%Y%m%d%H%M>-${slug}.org"
                      "#+TITLE: ${title}\n#+filetags:\n#+author: %(concat user-full-name)\n#+created: %u\n#+last_modified: %U\n\n"
           )
           :unnarrowed t)
          ("i" "inbox" plain "** TODO %? [/]" :if-new
           (file+head "tasks/inbox.org"
                      "#+TITLE: Inbox\n#+filetags: tasks\n#+author: %(concat user-full-name)\n#+created: %u\n#+last_modified: %U\n#+startup: overview\n#+archive: archives/${slug}-a.org::datetree/\n\n"
           )
           :unnarrowed t)
          ("t" "task" plain "** TODO %? [/]" :if-new
           (file+head "tasks/${slug}.org"
                      "#+TITLE: ${title}\n#+filetags: tasks\n#+author: %(concat user-full-name)\n#+created: %u\n#+last_modified: %U\n#+startup: overview\n#+archive: archives/${slug}-a.org::datetree/\n\n"
           )
           :unnarrowed t)
          ("r" "ref" plain "%?" :if-new
           (file+head "literature/${slug}.org"
                      ":PROPERTIES:\n:ROAM_REFS: ${ref}\n:END:\n#+TITLE: ${citekey}: ${title}\n#+filetags: literature\n#+author: %(concat user-full-name)\n#+created: %u\n#+last_modified: %U
- tags ::
- keywords :: ${keywords}
\n* ${title}
:PROPERTIES:
:Custom_ID: ${citekey}
:URL: ${url}
:AUTHOR: ${author-or-editor}
:NOTER_DOCUMENT: ${file}
:NOTER_PAGE:
:END:\n\n"
)
           :unnarrowed t)

          ("i" "incremental" plain "* %?" :if-new
           (file+head "websites/${slug}.org"
                      ":PROPERTIES:\n:ROAM_REFS:${ref}\n:END:\n#+TITLE: ${title}\n#+filetags: websites\n#+author: %(concat user-full-name)\n#+created: %u\n#+last_modified: %U\n- source :: ${ref}\n\n"
           )
           :unnarrowed t
           :empty-lines-before 1)
          ("l" "literature" plain "%?" :if-new
           (file+head "literature/%<%Y%m%d%H%M>-${slug}.org"
                      "#+TITLE: ${title}\n#+filetags: literature %^{roam_tags}\n#+author: %(concat user-full-name)\n#+created: %u\n#+last_modified: %U\n\n"
           )
           :unnarrowed t)
          ("f" "fleeting" plain "%?" :if-new
           (file+head "fleeting/%<%Y%m%d%H%M>-${slug}.org"
                      "#+TITLE: ${title}\n#+filetags:fleeting\n#+author: %(concat user-full-name)\n#+created: %u\n#+last_modified: %U\n\n"
           )
           :unnarrowed t)
          ("p" "Permanent (prompt folder)" plain "%?" :if-new
           (file+head "%(read-directory-name \"directory: \" org-directory)/%<%Y%m%d%H%M>-${slug}.org"
                      "#+TITLE: ${title}\n#+filetags: permanent\n#+author: %(concat user-full-name)\n#+created: %u\n#+last_modified: %U\n\n"
           )
           :unnarrowed t)))
  )
#+end_src
*** Org-roam hooks

A hook to update =last_modified= at save. Source =org-roam= discourse
#+begin_src elisp
;;--------------------------
;; Handling file properties for â€˜CREATEDâ€™ & â€˜LAST_MODIFIEDâ€™
;;--------------------------

(defun zp/org-find-time-file-property (property &optional anywhere)
  "Return the position of the time file PROPERTY if it exists.
    When ANYWHERE is non-nil, search beyond the preamble."
  (save-excursion
    (goto-char (point-min))
    (let ((first-heading
           (save-excursion
             (re-search-forward org-outline-regexp-bol nil t))))
      (when (re-search-forward (format "^#\\+%s:" property)
                               (if anywhere nil first-heading)
                               t)
        (point)))))

(defun zp/org-has-time-file-property-p (property &optional anywhere)
  "Return the position of time file PROPERTY if it is defined.
    As a special case, return -1 if the time file PROPERTY exists but
    is not defined."
  (when-let ((pos (zp/org-find-time-file-property property anywhere)))
    (save-excursion
      (goto-char pos)
      (if (and (looking-at-p " ")
               (progn (forward-char)
                      (org-at-timestamp-p 'lax)))
          pos
        -1))))

(defun zp/org-set-time-file-property (property &optional anywhere pos)
  "Set the time file PROPERTY in the preamble.
    When ANYWHERE is non-nil, search beyond the preamble.
    If the position of the file PROPERTY has already been computed,
    it can be passed in POS."
  (when-let ((pos (or pos
                      (zp/org-find-time-file-property property))))
    (save-excursion
      (goto-char pos)
      (if (looking-at-p " ")
          (forward-char)
        (insert " "))
      (delete-region (point) (line-end-position))
      (let* ((now (format-time-string "[%Y-%m-%d %a %H:%M]")))
        (insert now)))))

(defun zp/org-set-last-modified ()
  "Update the LAST_MODIFIED file property in the preamble."
  (when (derived-mode-p 'org-mode)
    (zp/org-set-time-file-property "last_modified")))
#+end_src

Add this hook
#+begin_src elisp
(add-hook 'before-save-hook #'zp/org-set-last-modified)
#+end_src

*** Org-roam-protocol

To register the =roam-protocol=, create a desktop application in =/usr/share/applications/org-protocol.desktop= or =~/.local/share/applications/org-protocol.desktop=
#+begin_src config :tangle no
[Desktop Entry]
Name=Org-Protocol
Exec=emacsclient %u
Icon=emacs-icon
Type=Application
Terminal=false
MimeType=x-scheme-handler/org-protocol
#+end_src

Associate =org-protocol://= links with the desktop file:
#+begin_src shell :tangle no
xdg-mime default org-protocol.desktop x-scheme-handler/org-protocol
#+end_src

TODO Customize graph view for =org-protocol=

*** Org-roam-ui

Settings for the =org-roam-ui=
#+begin_src elisp
(use-package! websocket
    :after org-roam)

(use-package! org-roam-ui
    :after org-roam
    :config
    (setq org-roam-ui-sync-theme t
          org-roam-ui-follow t
          org-roam-ui-update-on-save t
          org-roam-ui-open-on-start nil))
#+end_src

*** Searching and indexing files

Using =deft= to navigate =org-roam= files
#+begin_src elisp
(use-package deft
  :after org
  :bind
  ("C-c n d" . deft)
  :custom
  (deft-recursive t)
  (deft-use-filter-string-for-filename t)
  (deft-default-extension "org")
  (deft-directory "~/Documents/Org/Roam"))
#+end_src
*** Org-noter and org-roam-bibtex configuration

#+begin_quote
The following configuration was taken from [[https://www.ianjones.us/org-roam-bibtex][Ian Jones's org-roam-bibtex workflow]]
#+end_quote

Define some variables
#+begin_src elisp
(setq
 lit_notes "~/Documents/Org/Roam/literature"
 zot_bib "~/Documents/Org/library.bib")
#+end_src

Configuration for =org-noter= (this is already installed in ~Doom Emacs~)
#+begin_src elisp
(use-package! org-noter
  :after (:any org pdf-view)
  :config
  (setq
   ;; The WM can handle splits
   org-noter-notes-window-location 'horizontal-split
   ;; Please stop opening frames
   org-noter-always-create-frame nil
   ;; I want to see the whole file
   org-noter-hide-other nil
   ;; Everything is relative to the main notes file
   org-noter-notes-search-path (list lit_notes)
   )
  )
#+end_src

You might want to run =M-x pdf-tools-install= to view pdfs. Then set up the =org-pdftools= and =org-noter-pdftools= packages
#+begin_src elisp
(use-package org-pdftools
  :hook (org-load . org-pdftools-setup-link))
(use-package org-noter-pdftools
  :after org-noter
  :config
  (with-eval-after-load 'pdf-annot
    (add-hook 'pdf-annot-activate-handler-functions #'org-noter-pdftools-jump-to-note)))
#+end_src

Configuration for =helm-bibtex=
#+begin_src elisp
(setq
 bibtex-completion-notes-path lit_notes
 bibtex-completion-bibliography zot_bib
 bibtex-completion-pdf-field "file")
#+end_src

Finally, we configure =org-roam-bibtex=
#+begin_src elisp
(use-package! org-roam-bibtex
  :after (org-roam)
  :load-path zot_bib
  :hook (org-roam-mode . org-roam-bibtex-mode)
  :config
  (setq orb-preformat-keywords
        '(("citekey" . "=key=") "title" "url" "file" "author-or-editor" "keywords")
        orb-process-file-keyword t
        orb-file-field-extensions '("pdf")))
#+end_src

*** TODO org-roam-graph configuration
- source ::  https://www.tquelch.com/posts/emacs-config/#roam
** Org-transclusion

Transclude by reference
#+begin_src elisp
(use-package! org-transclusion
  :defer
  :after org
  :hook (org-roam-mode . org-transclusion-mode))
#+end_src

** Org-mode addons

*** Org-download

#+begin_src elisp
(use-package org-download
  :after org
  :bind
  (:map org-mode-map
   (("s-Y" . org-download-screenshot)
    ("s-y" . org-download-yank))))
#+end_src

*** TODO =mathpix.el=

#+begin_quote
Right now I think that this is unnecessary as I can just use the desktop app
#+end_quote

** Git-commit keybinding

Keybinding to commit =org= repository using script in =~/.dotfiles/bin/org-git-add-commit.sh=
#+begin_src elisp
(defun org-git-add-commit ()
  " Commit changes in the Org repo using a bash script "
  (interactive)
  (shell-command "bash ~/.dotfiles/bin/org-git-add-commit.sh"))

(map! :leader
      :prefix "z"
      :desc "org-git-add-commit" "c" #'org-git-add-commit)
#+end_src

** Keybinding to update title size
#+begin_src elisp
(defun org-title-style-update ()
  " Update org-document-title style"
    (set-face-attribute 'org-document-title nil
                        :height 1.9
                        :weight 'bold))

(map! :leader
      :prefix "z"
      :desc "org-title-style-update" "t" #'org-title-style-update)
#+end_src

** Org-books

Setting the path to the reading list org file
#+begin_src elisp
(setq org-books-file "~/Documents/Org/Roam/tasks/reading.org")
#+end_src

** Org-gcal

#+begin_src elisp
(after! org-gcal
  (add-hook 'org-agenda-mode-hook 'org-gcal-fetch)
  (setq org-gcal-client-id (password-store-get "cal/client-id")
        org-gcal-client-secret (password-store-get "cal/client-secret")
        org-gcal-recurring-events-mode 'nested
        org-gcal-up-days 30
        org-gcal-down-days 60
        org-gcal-fetch-file-alist '(("k.dileep1994@gmail.com" . "~/Documents/Org/Roam/calendars/personal_cal.org")
                                    ("dkishore@bu.edu" . "~/Documents/Org/Roam/calendars/bu_cal.org")
                                    ("42umeag2fj98antvq4k5va4g80@group.calendar.google.com" . "~/Documents/Org/Roam/calendars/startup_cal.org"))))
#+end_src

Add calendar files to =org-agenda=
#+begin_src elisp
(add-to-list 'org-agenda-files "~/Documents/Org/Roam/calendars")
#+end_src
** Org-toggl

Setting up org-toggl
#+begin_src elisp
(use-package! org-toggl
    :after org-clock
    :config
    (setq toggl-auth-token (password-store-get "toggl/api"))
    (setq org-toggl-inherit-toggl-properties t)
    (toggl-get-projects)
    (org-toggl-integration-mode))
#+end_src

** Org-Krita

Setting up =org-krita=
#+begin_src elisp
(use-package! org-krita
  :config
  (add-hook 'org-mode-hook 'org-krita-mode))
#+end_src

* Language specific configuration

** plantuml

Set up =plantuml= diagram support for =org-mode=
#+begin_src elisp
(after! org
  (setq org-plantuml-jar-path (expand-file-name "/usr/share/java/plantuml/plantuml.jar"))
  (add-to-list 'org-src-lang-modes '("plantuml" . plantuml))
  (org-babel-do-load-languages 'org-babel-load-languages '((plantuml . t)))
  )
#+end_src

** Julia

#+begin_src elisp
(setq lsp-julia-default-environment "~/.julia/environments/v1.6")
#+end_src
** Groovy and nextflow

#+begin_src elisp
(add-to-list 'auto-mode-alist '("\\.nf\\'" . groovy-mode))
#+end_src

* Credits

- [[https://tecosaur.github.io/emacs-config][Tecosaur's emacs config]]
- [[https://www.youtube.com/playlist?list=PLhXZp00uXBk4np17N39WvB80zgxlZfVwj][Doomcasts by Zaiste Programming]]
- [[https://www.ianjones.us/about-me][Ian Jones's blog]]
- [[https://gitlab.com/dwt1/dotfiles][Distrotube Derek Taylor's dotfiles]]
